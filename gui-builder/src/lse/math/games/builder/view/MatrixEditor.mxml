<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:local="."
			   title="Matrix editor"
			   close="promptExit()"
			   creationComplete="init()" >
	<fx:Script>
		<![CDATA[
			import flash.ui.Keyboard;
			
			import lse.math.games.builder.model.Outcome;
			import lse.math.games.builder.model.Player;
			import lse.math.games.builder.model.Rational;
			import lse.math.games.builder.model.StrategicForm;
			import lse.math.games.builder.model.Strategy;
			import lse.math.games.builder.presenter.Presenter;
			import lse.math.games.builder.viewmodel.AutoLabeller;
			
			
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.utils.StringUtil;
			
			import util.Log;
			import util.PromptThreeButtons;
			import util.PromptTwoButtons;
			
			
			private var _matrix:StrategicForm;
			private var _strMap:Dictionary;
			private var _payMap:Dictionary;
			private var _controller:Presenter;
			
			import lse.math.games.builder.settings.UserSettings;
			import lse.math.games.builder.settings.SCodes;
			
			private var log:Log = Log.instance;
			
			private var payoffMatrix1:Array;
			private var payoffMatrix2:Array;
			private var maxColumns:int;
			
			[Bindable]
			public var alpha1:String; //String with the strategies of pl1
			[Bindable]
			public var pl1NumStr:int;
			[Bindable]
			private var pl1PayMapStr:String;
			
			[Bindable]
			private var alpha2:String; //String with the strategies of pl2
			[Bindable]
			private var pl2NumStr:int;
			[Bindable]
			private var pl2PayMapStr:String;
			
			[Bindable]
			private var p2editable:Boolean=false;
			
			[Bindable]
			private var p2cbg:uint=0xffffff;
			
			private var glbSettings:UserSettings = UserSettings.instance;
			private var dataChanged:Boolean = false;
			private var instyle:String="ZG";
			
			
			
			/** Matrix which is being edited */
			public function set matrix(value:StrategicForm):void { 
				_matrix = value;
				
				_strMap = new Dictionary();
				_payMap = _matrix.payMatrixMap;
				
				for(var i:int = 0, pl:Player = _matrix.firstPlayer; i<_matrix.numPlayers; i++, pl=pl.nextPlayer)
					_strMap[pl] = _matrix.strategies(pl);
				
				populate(0);
				populate(1);
				onlyAlignStrategy()
				
			}
			
			private function init():void {
				pl1PayMapStrBox.addEventListener(KeyboardEvent.KEY_DOWN,keyPressedP1Payoff);
				pl2PayMapStrBox.addEventListener(KeyboardEvent.KEY_DOWN,keyPressedP2Payoff);
				setP2editable(false);
			}
			
			
			/** Presenter to which changes should be invalidated */
			public function set controller(value:Presenter):void {
				_controller = value;
				
			}

			public function setMode(isZero:Boolean):void {
				if (!isZero) {
					instyle="2G";
					p2editable=true;
				}
			}
			
			
			//Populates the matrix payoffs and strategies of player number 'plIdx' (starting in 0)
			//onto the graphical components
			private function populate(plIdx:int):void {
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				var pl:Player = _matrix.firstPlayer;
				for(var i:int = 0; i<plIdx; i++)
					pl = pl.nextPlayer;
				
				var strVec:Vector.<Strategy> = _strMap[pl];
				
				if(plIdx == 0)
				{
					alpha1 = strVec.join(delimeter);
					pl1NumStr = strVec.length;
					pl1PayMapStr = payoffMatrixString(pl);
				} else if(plIdx == 1)
				{
					alpha2 = strVec.join(delimeter);
					pl2NumStr = strVec.length;		
					pl2PayMapStr = payoffMatrixString(pl);
				}
			}
			
			//Update the alphabets from the text boxes and autolabelling the rest
			private function updateAlphabets():void {
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				var inUse:Vector.<String> = new Vector.<String>();
				
				var strVec1:Vector.<String> = new Vector.<String>();
				var strVec2:Vector.<String> = new Vector.<String>();
				
				for each(var st:String in pl1StratInput.text.split(delimeter)) {
					if(st != null && st.length>0) {
						inUse.push(st.toString().toLowerCase());
						strVec1.push(st);
					}
				}
				for each(st in pl2StratInput.text.split(delimeter)) {
					if(st != null && st.length>0) {
						inUse.push(st.toString().toLowerCase());
						strVec2.push(st);
					}
				}
				
				var al:AutoLabeller = new AutoLabeller();
				al.uniqueLabelNum = (pl1NumStr+pl2NumStr-strVec1.length-strVec2.length) > 0 ? 
					pl1NumStr+pl2NumStr-strVec1.length-strVec2.length : 0;
				
				alpha1 = "";
				for(var i:int = 0; i<pl1NumStr; i++)
				{
					var stratName:String;
					if(i<strVec1.length) 
						stratName = strVec1[i].toString()
					else {
						while(true){
							
			
							stratName = al.getNextAutoLabel_Player1(_matrix);
							if(inUse.indexOf(stratName.toLowerCase())<0)
								break;
						}
					}
					alpha1+= (stratName+delimeter);
				}
				
				alpha2 = "";
				for(i = 0; i<pl2NumStr; i++)
				{
					if(i<strVec2.length) 
						stratName = strVec2[i].toString()
					else {
						while(true){
							stratName = al.getNextAutoLabel_Player2(_matrix);
							if(inUse.indexOf(stratName.toLowerCase())<0)
								break;
						}
					}
					alpha2+= (stratName+delimeter);
				}
				
			}
			
			//Returns a string with the payoff matrix of a player
			
			private function payoffMatrixString(player:Player):String
			{
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				var pl1StrVec:Vector.<Strategy> = _strMap[_matrix.firstPlayer];
				var pl2StrVec:Vector.<Strategy> = _strMap[_matrix.firstPlayer.nextPlayer];
				
				var lines:Vector.<String> = new Vector.<String>();
				for each(var st1:Strategy in pl1StrVec)
				{
					var columns:Vector.<String> = new Vector.<String>();
					
					for each(var st2:Strategy in pl2StrVec)
					columns.push(_payMap[player][Strategy.key([st1,st2])]);
					
					lines.push(columns.join(delimeter));
				}
				return lines.join("\n");
			}
			
			
			
			/* <--- --- CLOSE HANDLERS --- ---> */	
			// Prompt if the user wants to exit saving changes
			private function promptExit():void {
				if (dataChanged) {
					PromptThreeButtons.show(resultFromExitPrompt, "Would you like to save the changes before closing?");
				}	else {
					exit();
				}
			}
			
			private function resultFromExitPrompt():void {
				if(PromptThreeButtons.buttonPressed == PromptThreeButtons.OK)
					saveChanges();
				else if(PromptThreeButtons.buttonPressed == PromptThreeButtons.DISCARD)
					exit();
			}
			
			// Save the changes into the new matrix
			private function saveChanges():void 
			{
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				alignStrategy();
				updateAlphabets();
				
				
				var firstPl:Player = _matrix.firstPlayer;
				_matrix.clearMatrix();
				_matrix.firstPlayer = firstPl;
				
				var rows:Vector.<Strategy> = new Vector.<Strategy>();
				var cols:Vector.<Strategy> = new Vector.<Strategy>();
				
				for each(var st:String in alpha1.split(delimeter))
				{
					if(st != null && st.length>0) {
						var strat:Strategy = new Strategy(firstPl);
						strat.name = st;
						rows.push(strat);
						_matrix.addStrategy(strat);
					}
				}
				
				for each(st in alpha2.split(delimeter))
				{
					if(st != null && st.length>0) {
						strat = new Strategy(firstPl.nextPlayer);
						strat.name = st;
						cols.push(strat);
						_matrix.addStrategy(strat);
					}
				}
				
				var lines1:Array = pl1PayMapStr.split("\n");
				var lines2:Array = pl2PayMapStr.split("\n");
				
				for each(strat in rows)
				{
					var columns1:Array = new Array();
					var line:String = lines1.shift() as String;
					if(line != null)
						columns1 = line.split(delimeter);
					
					var columns2:Array = new Array();
					line = lines2.shift() as String;
					if(line != null)
						columns2 = line.split(delimeter);
					
					for each(var strat2:Strategy in cols)
					{
						var payStr1:String;
						var payStr2:String;
						var outcome:Outcome = new Outcome();
						
						while(true) {
							payStr1 = trim(columns1.shift() as String);
							if(payStr1 != null && Rational.parse(payStr1) != Rational.NaN){
								outcome.setPay(firstPl, Rational.parse(payStr1));
								break;
							} else if(columns1.length==0){
								outcome.setPay(firstPl, Rational.NaN);
								break;
							}
						}
						
						while(true) {
							payStr2 = trim(columns2.shift() as String);
							if(payStr2 != null && Rational.parse(payStr2) != Rational.NaN){
								outcome.setPay(firstPl.nextPlayer, Rational.parse(payStr2));
								break;
							} else if(columns2.length==0) {
								outcome.setPay(firstPl.nextPlayer, Rational.NaN);
								break;
							}
						}
						_matrix.addPayoff([strat, strat2], outcome, Rational.ONE);
					}
				}
				
				//invalidate display
				if(_controller != null)
					_controller.invalidate(false, true, true); 
				else
					log.add(Log.ERROR_HIDDEN, "No controller defined ", "Settings");
				
				exit();
			}
			
			//Close the window
			private function exit():void
			{
				PopUpManager.removePopUp(this);
			}
			
			private function trim( s:String ):String
			{
				if (s!=null){
					if (Rational.parse(s) == Rational.NaN)
						return s;
					else
						return s.replace( /^([\s|\t|\n]+)?(.*)([\s|\t|\n]+)?$/gm, "$2" );
				} else{
					return s;
				}
			}
			
			/**
			 * Converts a string delimetered by returns (rows) and a column delimeter into an array (pm)
			 *@param inputString:String payoffss in string representation
			 *@param pm:Array - source array 
			 *@author Martin  
			 */	
			private function convertToMatrix(inputString:String,pm:Array):void {
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				var rows:Array = inputString.split("\n");
				var line:String = rows.shift() as String;
				maxColumns=0;
				while ( line != null ) {
					var columns:Array = line.split(delimeter);
					var column:String = columns.shift() as String;
					var matrixRow:Array=new Array();
					while (column != null) {
						if (trim(column) !="") {
							matrixRow.push(column);
						}
						column = columns.shift() as String;
					}
					if (matrixRow.length>maxColumns) {
						maxColumns=matrixRow.length;
					}
					if (matrixRow.length>0) {
						pm.push(matrixRow);
					}
					line = rows.shift() as String;
				}
			}
			
			
			/**
			 * Fills empty columns and rows with zeros. If (sym==true) this function creates a symmetric matrix and
			 * and fills empty elements with zeros.
			 *@param pm:Array - source array 
			 *@param sym:Boolean - if true than add columns or rows until pm is a symmetric array.
			 *@author Martin  
			 */	
			private function fillWithZeros(pm:Array,sym:Boolean):void{
				var i:int=0;
				var l:int=0;
				var k:int=0;
				for (i=0;i<pm.length;i++){
					l=pm[i].length;
					if (l<maxColumns) {
						for (k=0;k<maxColumns-l;k++) {
							pm[i].push("0");
						}
					}
				}
			
				//if game is symmetric rows and columns must have the same dimensions
				//Fill with zeros if not
				if (sym) {
					if (pm.length<maxColumns) {
						while (pm.length<maxColumns) {
							pm.push(new Array())
							for (k=0;k<maxColumns;k++) {
								pm[pm.length-1].push("0");
							}
						}
						
					} 
					
					if (pm.length>maxColumns){
						for (i=0;i<pm.length;i++){
							l=pm[i].length;
							if (l<pm.length) {
								for (var m:int=0;m<pm.length-l;m++) {
									pm[i].push("0");
								}
							}
						}
					}
					maxColumns=pm.length;
				}
				

			}
			
			/**
			 * Creates a negative array from pmFrom and copies it to pmTo
			 *@param pmFrom:Array - source array 
			 *@param pmTo:Array - destination array 
			 *@author Martin  
			 */	
			private function makeNegative(pmFrom:Array,pmTo:Array):void {
				for (var i:int=0;i<pmFrom.length;i++){
					var columns:Array=new Array();
					for (var j:int=0;j<pmFrom[i].length;j++){
						var r:Rational=Rational.parse(pmFrom[i][j]);
						if(pmFrom[i][j] != null && r != Rational.NaN){
							r=r.multiply(new Rational(-1,1));
							columns.push(r.toString());	
						}
						
						
					}
					pmTo.push(columns)
				}
			}
			
			protected function randomInt(max:Number):int
			{
				return int(Math.random() * max);
			}
			
			/**
			 * Generate random payoffs for array pmFrom and copy as negative numbers to array pmTo (copy==true) or create random
			 * payoffs for array pmTo (copy==false)
			 *@param pmFrom:Array - source array 
			 *@param pmTo:Array - destination array (negative copy of pmFrom or random payoffs)
			 *@param copy:Boolean - copy negativ array from pmFrom to pmTo (true) or create new random payoffs (false)   
			 *@author Martin  
			 */	
			private function makeRandom(pmFrom:Array,pmTo:Array,copy:Boolean):void {
				var pay2:Rational=null;
				for (var i:int=0;i<pmFrom.length;i++){
					var columns:Array=new Array();
					for (var j:int=0;j<pmFrom[i].length;j++){
						if (copy)
							pay2=Rational.parse(pmFrom[i][j]);
						else
							pay2=new Rational(randomInt(glbSettings.getValue("SYSTEM_MAX_PAYOFF") as Number), 1);
						
						if (instyle=="ZG")					
							pay2=pay2.negate();
						columns.push(pay2.toString());	
					}
					pmTo.push(columns)
				}
			}
			
			
			/**
			 * Generate random payoffs for player 1 and player 2 and aligns them
			 *@author Martin  
			 */	
			private function generateRandomPayoffs():void
			{
				payoffMatrix1=new Array();
				payoffMatrix2=new Array();
				
				convertToMatrix(pl1PayMapStr,payoffMatrix1);
				fillWithZeros(payoffMatrix1,false);
				makeRandom(payoffMatrix1,payoffMatrix2,false);
				payoffMatrix1=new Array();
				makeRandom(payoffMatrix2,payoffMatrix1,true);
				pl1PayMapStr=buildMatrixString(payoffMatrix1);
				pl2PayMapStr=buildMatrixString(payoffMatrix2);
			}
			
			
			/**
			 * Creates a transposed matrix from the array pmFrom and stores it to array pmTo
			 *@param pmFrom:Array - source Array
			 *@param pmTo:Array - destination array (transposed) 
			 *@author Martin  
			 */	
			private function makeTranspo(pmFrom:Array,pmTo:Array):void {
				
				for (var j:int=0;j<pmFrom[0].length;j++){
					var columns:Array=new Array();
					for (var i:int=0;i<pmFrom.length;i++){
						columns.push(pmFrom[i][j]);
					}
					pmTo.push(columns);
				}
			}
			
			
			/**
			 * Create a String from the array of payoffs
			 *@param pm:Array - 2-dim Array of payoffs
			 *@return String - an return seperated string with all payoffs.
			 *@author Martin  
			 */	
			private function buildMatrixString(pm:Array):String 
			{
				
				var delimeter:String=glbSettings.getValue(SCodes.EDITOR_MATRIX_DELIMETER) as String;
				var maxLength:Array = new Array();
				var i:int=0;
				var j:int=0;
				
				if (pm==null) 
					return Rational.NaN as String;
				if (pm[0]==null) 
					return Rational.NaN as String;
				
				for (j=0;j<pm[0].length;j++){
					var maxLen:int = 0;
					for (i=0;i<pm.length;i++){
						if (pm[i][j]!=null) {
							if (pm[i][j].length>maxLen) {
								maxLen=pm[i][j].length;
							}
							log.add(Log.DEBUG,pm[i][j]);
						}
					}
					maxLength.push(maxLen) as int;
				}
				
				var matrixString:String= "";
				for (i=0;i<pm.length;i++){
					for (j=0;j<pm[i].length;j++){
						matrixString += StringUtil.repeat(" ", maxLength[j] - pm[i][j].length ) + pm[i][j];
						if (j<pm[i].length-1){
							matrixString +=delimeter;
						}
						
					}
					if (i<pm.length - 1) {
						matrixString += "\n";
					}
				}
				return matrixString;
			
			}
			
			/**
			 * Align the matrix in columns and rows for player 1 and player 2
			 *  
			 *@author Martin  
			 */	
			private function onlyAlignStrategy():void 
			{
				payoffMatrix1=new Array();
				payoffMatrix2=new Array();
				
				convertToMatrix(pl1PayMapStr,payoffMatrix1);
				fillWithZeros(payoffMatrix1,false);
				pl1PayMapStr=buildMatrixString(payoffMatrix1)
				
				convertToMatrix(pl2PayMapStr,payoffMatrix2);
				fillWithZeros(payoffMatrix2,false);
				pl2PayMapStr=buildMatrixString(payoffMatrix2)
				
			}
			
			
			/**
			 * Align the matrix in columns and rows for player 1 and player 2 and update the strategy names
			 *  
			 *@author Martin  
			 */	
			private function alignStrategy():void 
			{
				
				payoffMatrix1=new Array();
				payoffMatrix2=new Array();

				
				if (instyle=="ZG") {
					convertToMatrix(pl1PayMapStr,payoffMatrix1);
					fillWithZeros(payoffMatrix1,false);
					makeNegative(payoffMatrix1,payoffMatrix2);
					pl1PayMapStr=buildMatrixString(payoffMatrix1)
					pl2PayMapStr=buildMatrixString(payoffMatrix2)
					
					
				} else if (instyle=="SG") {
					convertToMatrix(pl1PayMapStr,payoffMatrix1);
					fillWithZeros(payoffMatrix1,true);
					makeTranspo(payoffMatrix1,payoffMatrix2);
					pl1PayMapStr=buildMatrixString(payoffMatrix1)
					pl2PayMapStr=buildMatrixString(payoffMatrix2)
					
				} else {
					convertToMatrix(pl1PayMapStr,payoffMatrix1);
					fillWithZeros(payoffMatrix1,false);
					pl1PayMapStr=buildMatrixString(payoffMatrix1)
						
						
					convertToMatrix(pl2PayMapStr,payoffMatrix2);
					fillWithZeros(payoffMatrix2,false);
					pl2PayMapStr=buildMatrixString(payoffMatrix2)
					
					
				}
				
				
				pl1NumStr = payoffMatrix1.length;
				if (payoffMatrix2.length>0) {
					pl2NumStr = payoffMatrix2[0].length;
				}else {
					pl2NumStr =0;
				}
				updateAlphabets()
				
			}

			
			/**
			 * Sets the current status of the editor depending on the radiobutton. Sets the member variable <instyle> and enables or disables the matrix of player 2
			 *@param arg:String (ZG=ZeroSum Game,SG=Symetric Game, 2G=General Game)  
			 *@author Martin  
			 */	
			private function changeEvtRadioButton(arg : String):void {
				instyle=arg;
				if ((instyle=="ZG") || (instyle=="SG")) {
					setP2editable(false);
					
				} else {
					setP2editable(true);
				}
				
				alignStrategy();
				
			}
			
			
			/**
			 * Controls wether matrix of player 2 is editable and it changes the background color ot the textarea of the matrix
			 * <ul>
			 * <li> true: textarear of plaer 2 matrix is editable and background color is white
			 * <li> true: textarear of plaer 2 matrix is NOT editable and background color is gray (#ccc)
			 * </ul>
			 *@param editable:Boolean 
			 *@author Martin  
			 */	
			private function setP2editable(editable:Boolean):void {
				if (editable==true){
					p2editable=true;
					p2cbg=0xffffff;
				} else {
					p2editable=false;
					p2cbg=0xcccccc;
					
				}
			
			}
			
			/**
			 * Event catches the return event to align the matrix of player 1 after a new row is inserted 
			 *@author Martin  
			 */	
			private function keyPressedP1Payoff(event:KeyboardEvent):void{
				
				if ((instyle!="SG") && (event.keyCode==13)){
					//It does not make sense to autoalign in a symmetric game during typing
					alignStrategy();
					pl1PayMapStrBox.text=pl1PayMapStrBox.text+"\n";
					pl1PayMapStrBox.selectRange(pl1PayMapStrBox.text.length,pl1PayMapStrBox.text.length);
				}
			}
			
			/**
			 * Event catches the return event to align the matrix of player 2 after a new row is inserted 
			 *@author Martin  
			 */	
			private function keyPressedP2Payoff(event:KeyboardEvent):void{
				
				if ((instyle!="SG") && (event.keyCode==13)){
					//It does not make sense to autoalign in a symmetric game during typing
					alignStrategy();
					pl2PayMapStrBox.text=pl2PayMapStrBox.text+"\n";
					pl2PayMapStrBox.selectRange(pl2PayMapStrBox.text.length,pl2PayMapStrBox.text.length);
				}
			}

			
		]]>
	</fx:Script>

	<fx:Style>
		@font-face {
			src:url("../../../../../../assets/fonts/COURBD.TTF");
			fontFamily: myFontFamily;
			advancedAntiAliasing: true;
		}
	</fx:Style>

	<s:VGroup width="819" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
		<s:HGroup width="100%" >
			<s:Label y="0" width="90" height="20" text="Type of game: " verticalAlign="middle"/>
			<s:RadioButton groupName="rbg" id="rgb_ZG" label="Zerosum game (A,-A)"  selected="true" click="changeEvtRadioButton('ZG');dataChanged=true;" />
			<s:RadioButton groupName="rbg" id="rgb_SG" label="Symmetric game (A,AT)" click="changeEvtRadioButton('SG');dataChanged=true;" />
			<s:RadioButton groupName="rbg" id="rgb_2G" label="General game (A,B)"   click="changeEvtRadioButton('2G');dataChanged=true;" />
			<s:Button label="Random Payoffs" click="generateRandomPayoffs();" />
		</s:HGroup>
		<s:HGroup width="100%" gap="0">
			<s:VGroup width="50%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">

				<s:HGroup  width="100%" verticalAlign="middle">
					<s:Label height="20" width="90" text="Strategy names: " verticalAlign="middle"/>
					<s:TextInput id="pl1StratInput" width="100%" editable="true" widthInChars="22" text="{alpha1}" change="dataChanged=true;" />
				</s:HGroup>	
				<s:Label height="20" color="#2323E6" text="Player 1 PayOff Matrix"
						 verticalAlign="middle"/>
				<s:TextArea id="pl1PayMapStrBox" width="100%" widthInChars="30" fontFamily="myFontFamily" heightInLines="15" lineBreak="explicit" 
							text="{pl1PayMapStr}" change="pl1PayMapStr = pl1PayMapStrBox.text;dataChanged=true;"/>
				<s:HGroup width="100%" horizontalAlign="center">
					<s:Button label="Reset" cornerRadius="0" click="populate(0);"/>
					<s:Button label="Align and Update" cornerRadius="0" click="alignStrategy();"/>
				</s:HGroup>
			</s:VGroup>
			<s:VGroup width="50%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
				<!--
					<mx:Spacer height="20"/>
				-->
				<s:HGroup width="100%" verticalAlign="middle">
					<s:Label height="20" width="90" text="Strategy names:" verticalAlign="middle"/>
					<s:TextInput editable="{p2editable}" width="100%" id="pl2StratInput" widthInChars="22" text="{alpha2}" change="dataChanged=true;" />
				</s:HGroup>
				<s:Label height="20" color="#2323E6" text="Player 2 PayOff Matrix"
						 verticalAlign="middle"/>
				<s:TextArea editable="{p2editable}" id="pl2PayMapStrBox" width="100%" widthInChars="30" fontFamily="myFontFamily" heightInLines="15" lineBreak="explicit" 
							text="{pl2PayMapStr}" change="pl2PayMapStr = pl2PayMapStrBox.text;dataChanged=true;" contentBackgroundColor="{p2cbg}" />
				<s:HGroup width="100%" horizontalAlign="center">
					<s:Button enabled="{p2editable}" label="Reset" cornerRadius="0" click="populate(1);" />
				</s:HGroup>
			</s:VGroup>
		</s:HGroup>

		<s:HGroup width="100%" horizontalAlign="center" paddingBottom="5" paddingTop="5">
			<s:Button label="Close and save changes" cornerRadius="0" width="200" click="saveChanges();"/>
			<s:Button label="Close and discard changes" cornerRadius="0" width="200" click="exit()"/>
		</s:HGroup>

		
	</s:VGroup>
	
</s:TitleWindow>
